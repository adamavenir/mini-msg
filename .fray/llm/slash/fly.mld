import { @agent } from @payload

exe @fray(args) = cmd { fray @args --json }
exe @safe(obj, field) = js { return (obj && obj[field]) || [] }
exe @at(name) = js { return "@" + name }

>> Pull context from fray
var @notes = @fray(`get meta/@agent/notes --last 5`)
var @meta = @fray(`get meta --last 10`)
var @mentions = @fray(`@$@agent --last 10`)
var @claimsData = @fray(`claims @$@agent`)
var @ready = cmd { bd ready }

>> Extract arrays with fallbacks
var @notesList = @safe(@notes, "messages")
var @metaList = @safe(@meta, "messages")
var @mentionsList = @safe(@mentions, "mentions")
var @claimsList = @safe(@claimsData, "claims")
var @readyList = @ready

var @context = `**You are @at(@agent).** Check fray for context.

## Your Notes (prior session handoffs)
/for @m in @notesList
- [@m.id] @m.body
/end

## Project Meta (shared context)
/for @m in @metaList
- [@m.id] @m.from_agent: @m.body
/end

## Recent @mentions
/for @m in @mentionsList
- [@m.id] @m.from_agent: @m.body
/end

## Your Claims
/for @c in @claimsList
- @c.resource
/end

## Ready Work (bd ready)
/for @i in @readyList
- [@i.id] @i.title
/end

---
Run: fray get @at(@agent)

IMPORTANT: Users only see messages posted to fray. Your stdout is not visible. Post progress updates and summaries to fray so users can follow your work.`

show @context

export { @context }
